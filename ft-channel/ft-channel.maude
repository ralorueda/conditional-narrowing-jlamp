load smt

fmod INTEGER-LIST is 
    protecting REAL-INTEGER .

    sorts IntegerList . 
    subsorts Integer < IntegerList .

    op nil : -> IntegerList [ctor] .
    op _;_ : IntegerList IntegerList -> IntegerList [ctor assoc] .

    vars L Q : IntegerList .

    eq L ; nil = L [variant] .
    eq nil ; L = L [variant] .
    eq L ; nil ; Q = L ; Q [variant] . *** B-coherence extension
endfm


mod FT-CHANNEL is 
    protecting INTEGER-LIST .
    protecting BOOL-OPS .

    sorts Msg MsgSet Channel .
    subsorts Msg < MsgSet .

    op null : -> MsgSet [ctor] .
    op __ : MsgSet MsgSet -> MsgSet [ctor assoc comm] .
    op [_,_]_[_,_] : IntegerList Integer MsgSet IntegerList Integer -> Channel [ctor] .
    op {_,_} : Integer Integer -> Msg [ctor] .
    op ack : Integer -> Msg [ctor] .
    op empty : MsgSet -> Bool .

    vars N M J K : Integer .    vars L P Q R : IntegerList .
    var MSG : Msg .    vars S S' : MsgSet .    vars CH CH' : Channel .

    eq S null = S [variant] .
    eq S S = S [variant] . *** set idempotency
    eq S S S' = S S' [variant] . *** B-coherence extension

    eq empty(null) = true [variant] .
    eq empty(MSG S) = false [variant] .

    rl [send] : [J ; L,N] S [P,M] => [J ; L,N] {J,N} S [P,M] [narrowing] .

    crl [recv-1] : [L,N] {J,K} S [P,M] => [L,N] S ack(K) [P ; J, M + 1] 
                    if (K === M) = true .

    crl [recv-2] : [L,N] {J,K} S [P,M] => [L,N] S ack(K) [P,M] 
                    if (K =/== M) = true .

    crl [ack-recv-1] : [J ; L,N] ack(K) S [P,M] => [L,N + 1] S [P,M] 
                    if (K === N) = true .

    crl [ack-recv-2] : [J ; L,N] ack(K) S [P,M] => [J ; L,N] S [P,M] 
                    if (K =/== N) = true .

    crl [loss] : [L,N] S S' [P,M] => [L,N] S' [P,M] 
                    if empty(S) = true .
endm

eof

--- Experiments commands
vu-narrow [,2] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
{canonical} vu-narrow [,2] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
vu-narrow [,3] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
{canonical} vu-narrow [,3] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
vu-narrow [,4] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
{canonical} vu-narrow [,4] in FT-CHANNEL : [NL:IntegerList, N:Integer] null [NL':IntegerList, N':Integer] =>* C:Channel such that (true).Boolean .
